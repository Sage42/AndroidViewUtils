// Project properties
project.group = 'com.sage42.android.view'
project.version = '1.0.1'

apply plugin: 'android-library'
apply plugin: 'maven'
apply plugin: 'signing'

repositories
{
    mavenLocal()
    mavenCentral()
}

android
{
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    defaultConfig
    {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
    }

    sourceSets
    {
        main
        {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src/main/java']
            resources.srcDirs = ['src/main/resources']
            aidl.srcDirs = ['src/main/aidl']
            renderscript.srcDirs = ['src/main/rs']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
    }
}

configurations
{
    archives
    { extendsFrom configurations.default }
}

signing
{
    required
    {
        has("release") && gradle.taskGraph.hasTask("uploadArchives")
    }
    sign configurations.archives
}

def localRepoPath = "file://" +  new File(System.getProperty("user.home"), ".m2/repository").absolutePath

uploadArchives 
{
    configuration = configurations.archives
    repositories.mavenDeployer 
    {
        beforeDeployment 
        { 
            MavenDeployment deployment -> signing.signPom(deployment) 
        }

        repository(url: localRepoPath) 

        pom.project 
        {
            packaging 'aar'

            licenses 
            {
                license 
                {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
        }
    }
}